Grammar for the Cthulhu Assembler
Scot W. Stevenson <scot.stevenson@gmail.com>
First version: 07. Mai 2018
This version: 13. Mai 2018

We use EBNF as used by Go (see https://golang.org/ref/spec#Notation and
http://www.cs.sfu.ca/CourseCentral/383/tjd/ebnf_intro_in_go.html) for
our language description.

// A line can begin with one of four types:

statement = comment | directive | label | instruction

comment_line = ";" { anything } "<EOL>"
comment = { anything } ";" { anything } "<EOL>"

directive = "." ( letter | dec_digit | "!" | "*" ) { letter | dec_digit | "!" | "*"  }
        for one of:

        ".*"
        ".a8"
        ".a16"
        ".axy16"
        ".advance" expr
        ".assert" "a8" | "a16" | "xy8" | "xy16" | "native" | "emulated"
        ".bank" expr
        ".byte" expr { "," expr } [ comment ] "<EOL>"
        ".emulate"
        ".end"
        ".equ" symbol expr
        ".here"
        ".include" string
        ".long" expr { "," expr }
        ".lsb" expr
        ".msb" 
        ".mpu" string, one of "6502" | "65c02" | "65816"
        ".native"
        ".origin" expr
        ".ram" expr expr
        ".rom" expr expr
        ".status"
        ".word" expr { "," expr } [ comment ] "<EOL>"
        ".xy16"
        ".xy8"
        

// Labels 

label = ( local_label | global_label | anon_label ) [ directive | instruction ] [ comment ]
local_label = "_" symbol ":" 
global_label = symbol ":" 
anon_label = "@"

symbol = letter { letter | digit | sym_special }
sym_special = "!" | "?" | "_" | "&" | "'" | "#" | "." | "~" | "^" | "&" | "=" | "|"

// Instruction

instruction_san = ( opc_san_0 [ comment ] ) |
                  ( opc_san_1 operand_san [ comment ] ) |
                  ( opc_san_2 operand_san "," operand_san [ comment ] )

operand_san = expr

single_operator = ".lsb" | ".msb" | ".bank" | ".not"
binary_operator = ".and" | ".or" | ".xor" | "+" | "-" | "/" | "*" | ".lshift" | 
        ".rshift" | "%"

// Expression

value = number | symbol | rpn

expr =  value | single_operator value | value binary_operator value [ comment ] <EOL>


// Reverse Polish Notation (RPN) 

rpn = "{" value { value | prn_operator } "}" [ comment ] 

rpn_operator = ".drop" | ".dup" | ".lsb" | ".msb" | ".bank" | "+" | "*" | "-" | "/" |
        ".and" | ".or" | ".xor" | ".not" | ".invert" | ".lshift" | ".rshift" | "%" |
        ".swap"


// Numbers

number = bin_number | dec_number | hex_number 

bin_number = '%' bin_digit { bin_digit | bin_special }
bin_special = ":" | "." 

dec_number = dec_digit { dec_digit }
dec_digit = "0" ... "9"

hex_number = '$' hex_digit { hex_digit | hex_special }
hex_special = ":" | "." 
hex_digit = dec_digit | "a" ... "f" | "A" ... "F"


// Basic types

string = "\"" { any_unicode } "\""
letter = "a" ... "z" | "A" ... "Z"


==== TYPES ====

In addition, numbers have types, which can be addresses, values, or offsets.

        addr = addr8 | addr16 | addr24
        data = data8 | data16 | data24 
        offset = offset_small | offset_large

